plugins {
    id 'org.jetbrains.intellij' version '0.4.18'
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
}

group 'io.github.stefansjs.flatbuffersplugin'
version "0.4-alpha+${getBuildNumber()}"

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.jetbrains.kotlin:kotlin-test-testng"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

intellij {
    version '2018.2'
    alternativeIdePath = findProperty('io.github.stefansjs.flatbuffersplugin.idePath')
    downloadSources true
    updateSinceUntilBuild false
}

sourceSets.main.java.srcDir 'src/main/gen'

test {
}

def getGitHash() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

String getBuildNumber() {
    if(System.getenv("GITHUB_RUN_NUMBER") != null) {
        System.getenv("GITHUB_RUN_NUMBER")
    } else {
        "${getGitHash()}"
    }
}
